
&НаКлиенте
Процедура Сформировать(Команда)
	//Достаем из формы период
	НачалоПериода = ДатаНачалаПериода;
	КонецПериода = ДатаОкончанияПериода;
	
	//Используем функцию для построения отчета
	РезультатОтчета = ПолучитьТабличныйДокумент(НачалоПериода, КонецПериода); 
	ТабличныйДокумент = РезультатОтчета;
КонецПроцедуры


&НаСервере
Функция ПолучитьТабличныйДокумент(НачалоПериода, КонецПериода)
	
	ТекОтчет = РеквизитФормыВЗначение("Отчет");
	
	Макет = ТекОтчет.ПолучитьМакет("Макет");
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	//Выводим ячейку левый верхний угол
	Область = Макет.ПолучитьОбласть("Шапка|Товары");
	ОбластьШапкаСклад = Макет.ПолучитьОбласть("Шапка|Склад");
	ОбластьШапкаИтого = Макет.ПолучитьОбласть("Шапка|Итого");
	ТабличныйДокумент.Вывести(Область);
	
	
	
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПартииТоваровНаСкладахОстаткиИОбороты.Номенклатура КАК Товар,
	               |	ПартииТоваровНаСкладахОстаткиИОбороты.Склад КАК Склад,
	               |	СУММА(ПартииТоваровНаСкладахОстаткиИОбороты.КоличествоНачальныйОстаток) КАК НачальныйОстаток,
	               |	СУММА(ПартииТоваровНаСкладахОстаткиИОбороты.КоличествоКонечныйОстаток) КАК КонечныйОстаток,
	               |	СУММА(ПартииТоваровНаСкладахОстаткиИОбороты.КоличествоПриход) КАК КоличествоПриход,
	               |	СУММА(ПартииТоваровНаСкладахОстаткиИОбороты.КоличествоРасход) КАК КоличествоРасход,
	               |	СУММА(ПартииТоваровНаСкладахОстаткиИОбороты.СуммаПриход) КАК СуммаПриход
	               |ИЗ
	               |	РегистрНакопления.ПартииТоваровНаСкладах.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, , , ) КАК ПартииТоваровНаСкладахОстаткиИОбороты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПартииТоваровНаСкладахОстаткиИОбороты.Номенклатура,
	               |	ПартииТоваровНаСкладахОстаткиИОбороты.Склад
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ПартииТоваровНаСкладахОстаткиИОбороты.Номенклатура
	               |ИТОГИ
	               |	СУММА(НачальныйОстаток),
	               |	СУММА(КонечныйОстаток),
	               |	СУММА(КоличествоПриход),
	               |	СУММА(КоличествоРасход),
	               |	СУММА(СуммаПриход)
	               |ПО
	               |	Товар ИЕРАРХИЯ,
	               |	Склад";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(НачалоПериода));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(КонецПериода));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	//Цикл для вывода складов в шапку
	ВыборкаСклад = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Склад");
	Пока ВыборкаСклад.Следующий() Цикл
		ОбластьШапкаСклад.Параметры.Заполнить(ВыборкаСклад);
		ТабличныйДокумент.Присоединить(ОбластьШапкаСклад);
	КонецЦикла;
	
	ТабличныйДокумент.Присоединить(ОбластьШапкаИтого);
	
	
	
	ОбластьСтрокаТовары = Макет.ПолучитьОбласть("Строка|Товары");
	ОбластьСтрокаСклад = Макет.ПолучитьОбласть("Строка|Склад");
	ОбластьСтрокаИтого = Макет.ПолучитьОбласть("Строка|Итого");
	
	
	//Из этой выборки будем доставать товары и итоги
	ВыборкаТовар = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Товар");
	//Автогруппировка строк для товаров по иерархии
	ТабличныйДокумент.НачатьАвтогруппировкуСтрок();
	
	
	
	Пока ВыборкаТовар.Следующий() Цикл
		//Располагаем товары в зависимости от положения в иерархии
		ОбластьСтрокаТовары.Параметры.Заполнить(ВыборкаТовар);
		Если ВыборкаТовар.Уровень() = 0 Тогда
			ТабличныйДокумент.Вывести(ОбластьСтрокаТовары, 0);
		Иначе
			ТабличныйДокумент.Вывести(ОбластьСтрокаТовары, 1);
		КонецЕсли;
		
		//Из этой выборки достанем детализацию ресурсов по складам
		ВыборкаСклад = ВыборкаТовар.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Склад", "ВСЕ");
		
		
		Пока ВыборкаСклад.Следующий() Цикл
			//Присоединяем справа к строкам товаров ресурсы
			ОбластьСтрокаСклад.Параметры.Заполнить(ВыборкаСклад);
			ТабличныйДокумент.Присоединить(ОбластьСтрокаСклад);
			
			
			
		КонецЦикла;
		
		//После прохода по всем складам выводим итоги по номенклатуре
		ОбластьСтрокаИтого.Параметры.Заполнить(ВыборкаТовар);
		ТабличныйДокумент.Присоединить(ОбластьСтрокаИтого);
		
			
		
	КонецЦикла;
	
	ТабличныйДокумент.ЗакончитьАвтогруппировкуСтрок();
	
	
	Возврат ТабличныйДокумент;
	
       
КонецФункции
